(load "base.scm")
(define (rember* a l)
  (cond ((null? l) '())
        ((eq? a (car l)) (rember* a (cdr l)))
        ((not (atom? (car l))) (cons (rember* a (car l))
                                     (rember* a (cdr l))))
        (else (cons (car l) (rember* a (cdr l))))))

(define (insertR* new old l)
  (cond ((null? l) '())
        ((atom? (car l))
         (cond
           ((eq? old (car l)) (cons old (cons new (insertR* new old (cdr l)))))
           (else (cons (car l) (insertR* new old (cdr l))))))
        (else (cons (insertR* new old (car l))
                    (insertR* new old (cdr l))))))

(load "numbers.scm")
(define (occur* a l)
  (cond ((null? l) 0)
        ((atom? (car l)) (if (eq? a (car l))
                             (add1 (occur* a (cdr l)))
                             (occur* a (cdr l))))
        (else (add (occur* a (car l))
                   (occur* a (cdr l))))))

(define (subst* new old l)
  (cond ((null? l) '())
        ((atom? (car l))
         (if (eq? old (car l))
             (cons new (subst* new old (cdr l)))
             (cons (car l) (subst* new old (cdr l)))))
        (else (cons (subst* new old (car l))
                    (subst* new old (cdr l))))))

(define (insertL* new old l)
  (cond ((null? l) '())
        ((atom? (car l))
         (if (eq? old (car l))
             (cons new (cons old (insertL* new old (cdr l))))
             (cons (car l) (insertL* new old (cdr l)))))
        (else (cons (insertL* new old (car l))
                    (insertL* new old (cdr l))))))

(define (member* a l)
  (cond ((null? l) #f)
        ((atom? (car l))
         (or (eq? a (car l)) (member* a (cdr l))))
        (else (or (member* a (car l))
                  (member* a (cdr l))))))

(define (leftmost l)
  (if (atom? (car l))
      (car l)
      (leftmost (car l))))

(define (eqlist? l1 l2)
  (cond ((null? l1) (null? l2))
        ((null? l2) #f)
        (else (and (equal? (car l1) (car l2))
                   (eqlist? (cdr l1) (cdr l2))))))

(define (equal? s1 s2)
  (cond ((and (atom? s1) (atom? s2))
         (eqan? s1 s2))
        ((or (atom? s1) (atom? s2)) #f)
        (else (eqlist? s1 s2))))

(define (rember s l)
  (cond ((null? l) '())
        ((equal? s (car l)) (cdr l))
        (else (cons (car l) (rember s (cdr l))))))
